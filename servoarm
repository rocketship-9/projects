#include <Servo.h>

int clawPin = 2;
Servo servoClaw;

Servo servoA; 
Servo servoB;

const int downA = 13;
const int upA = 12;
const int downB = 6;
const int upB = 3;

const float speed = 60.0;
const float minAngle = 20.0;
const float maxAngle = 160.0;

float currentAngleA = 90.0;
float currentAngleB = 90.0;

unsigned long lastTime = 0;

const int motorPins[4] = {8, 9, 10, 11}; // IN1-IN4
const int joyXPin = A3;
const int center = 512;
const int deadzone = 40; // wider deadzone to prevent drift

int stepSequence[8][4] = {
  {1,0,0,0},
  {1,1,0,0},
  {0,1,0,0},
  {0,1,1,0},
  {0,0,1,0},
  {0,0,1,1},
  {0,0,0,1},
  {1,0,0,1}
};

int currentStep = 0;

unsigned long lastStepTime = 0;
int stepDelay = 1500; // microseconds (start slower)

// function prototypes
void stepForward();
void stepBackward();


void setup() 
{
  pinMode(clawPin, INPUT_PULLUP);
  servoClaw.attach(5);

  pinMode(downA, INPUT_PULLUP);
  pinMode(upA, INPUT_PULLUP);
  pinMode(downB, INPUT_PULLUP);
  pinMode(upB, INPUT_PULLUP);

  servoA.attach(4);
  servoB.attach(7);

  servoA.write(currentAngleA);
  servoB.write(currentAngleB);

  lastTime = millis();

   for (int i = 0; i < 4; i++) {
    pinMode(motorPins[i], OUTPUT);
  }
}

void loop() 
{
  unsigned long now = millis();
  float dt = (now - lastTime) / 1000.0;
  lastTime = now;

  int directionA = 0;
  int directionB = 0;

  if (digitalRead(upA) == LOW)
  {
    directionA = +1;
  }
  else if (digitalRead(downA) == LOW)
  {
    directionA = -1;
  }

  if (digitalRead(upB) == LOW)
  {
    directionB = +1;
  }
  else if (digitalRead(downB) == LOW)
  {
    directionB = -1;
  }

  float deltaAngleA = directionA * speed * dt;
  float nextAngleA = currentAngleA + deltaAngleA;

  float deltaAngleB = directionB * speed * dt;
  float nextAngleB = currentAngleB + deltaAngleB;
  
  if (nextAngleA > maxAngle)
  {
    nextAngleA = maxAngle;
  }
  else if (nextAngleA < minAngle)
  {
    nextAngleA = minAngle;
  }

  if (nextAngleB > maxAngle)
  {
    nextAngleB = maxAngle;
  }
  else if (nextAngleB < minAngle)
  {
    nextAngleB = minAngle;
  }

  currentAngleA = nextAngleA;
  currentAngleB = nextAngleB;

  servoA.write(currentAngleA);
  servoB.write(currentAngleB);

  
  //claw
  if (digitalRead(clawPin) == LOW) 
  {
    servoClaw.write(125);
  } 

  else 
  {
    servoClaw.write(180);
  }

  int sum = 0;
  for (int i = 0; i < 5; i++) sum += analogRead(joyXPin);
  int jX = sum / 5;

  // Direction
  int dir = 0;
  if (jX > center + deadzone) dir = 1;
  else if (jX < center - deadzone) dir = -1;

  // Speed mapping
  if (dir != 0) {
      int effort = abs(jX - center);
      stepDelay = map(effort, deadzone, 512, 2000, 500); // slow â†’ fast
  }

  // NON-BLOCKING STEPPER
  if (dir != 0 && micros() - lastStepTime >= stepDelay) {
      if (dir == 1) stepForward();
      else stepBackward();
      lastStepTime = micros();
  }

}

void stepForward() {
  currentStep = (currentStep + 1) % 8;
  for (int i = 0; i < 4; i++) {
    digitalWrite(motorPins[i], stepSequence[currentStep][i]);
  }
}

void stepBackward() {
  currentStep = (currentStep + 7) % 8;
  for (int i = 0; i < 4; i++) {
    digitalWrite(motorPins[i], stepSequence[currentStep][i]);
  }
}
