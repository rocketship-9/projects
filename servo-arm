#include <Servo.h>

int clawPin = 2;
Servo servoClaw;

Servo servoA; 
Servo servoB;

// button definitions to move servo A (12/13) and B (6/3) up or down
const int downA = 13;
const int upA = 12;
const int downB = 6;
const int upB = 3;

// servo movement speed and electronic limits
const float speed = 60.0;
const float minAngle = 20.0;
const float maxAngle = 160.0;

// initializes angles
float currentAngleA = 90.0;
float currentAngleB = 90.0;

// last timestamp for time difference calculation
unsigned long lastTime = 0;

// defines stepper motor and joystick control pins
const int motorPins[4] = {8, 9, 10, 11}; // IN1-IN4
const int joyXPin = A3;

// sets 512 as joystick center (between 0 & 1023) and creates a deadzone to prevent drift
const int center = 512;
const int deadzone = 40;

int stepSequence[8][4] = {
  {1,0,0,0},
  {1,1,0,0},
  {0,1,0,0},
  {0,1,1,0},
  {0,0,1,0},
  {0,0,1,1},
  {0,0,0,1},
  {1,0,0,1}
};

// tracks step in the sequence and delay between steps
int currentStep = 0;
unsigned long lastStepTime = 0;
int stepDelay = 1500;

void stepForward();
void stepBackward();


void setup() 
{
  pinMode(clawPin, INPUT_PULLUP);
  servoClaw.attach(5);

  pinMode(downA, INPUT_PULLUP);
  pinMode(upA, INPUT_PULLUP);
  pinMode(downB, INPUT_PULLUP);
  pinMode(upB, INPUT_PULLUP);

  servoA.attach(4);
  servoB.attach(7);

  servoA.write(currentAngleA);
  servoB.write(currentAngleB);

  lastTime = millis();

  // sets the stepper motor pins as outputs
  for (int i = 0; i < 4; i++) 
  {
    pinMode(motorPins[i], OUTPUT);
  }
}

void loop() 
{
  // gets current time and calculates change in time (dt) since last loop
  unsigned long now = millis();
  float dt = (now - lastTime) / 1000.0;
  lastTime = now;

  // direction variables for servos (+1 = up, -1 = down)
  int directionA = 0;
  int directionB = 0;

  if (digitalRead(upA) == LOW)
  {
    directionA = +1;
  }
  else if (digitalRead(downA) == LOW)
  {
    directionA = -1;
  }

  if (digitalRead(upB) == LOW)
  {
    directionB = +1;
  }
  else if (digitalRead(downB) == LOW)
  {
    directionB = -1;
  }

  // calculates how much each servo should move based on direction speed and time passed
  float deltaAngleA = directionA * speed * dt;
  float nextAngleA = currentAngleA + deltaAngleA;

  float deltaAngleB = directionB * speed * dt;
  float nextAngleB = currentAngleB + deltaAngleB;

  // applies electronic limits to both servos
  if (nextAngleA > maxAngle)
  {
    nextAngleA = maxAngle;
  }
  else if (nextAngleA < minAngle)
  {
    nextAngleA = minAngle;
  }

  if (nextAngleB > maxAngle)
  {
    nextAngleB = maxAngle;
  }
  else if (nextAngleB < minAngle)
  {
    nextAngleB = minAngle;
  }

  // updates angles and tells servos to move to the new positions
  currentAngleA = nextAngleA;
  currentAngleB = nextAngleB;

  servoA.write(currentAngleA);
  servoB.write(currentAngleB);

  
  // claw
  if (digitalRead(clawPin) == LOW) 
  {
    servoClaw.write(125);
  } 

  else 
  {
    servoClaw.write(180);
  }

  // reads joystick and averages values to reduce noise
  int sum = 0;
  for (int i = 0; i < 5; i++) sum += analogRead(joyXPin);
  int jX = sum / 5;

  // determines direction of joystick (right = 1, left = -1, center = 0)
  int dir = 0;
  if (jX > center + deadzone) dir = 1;
  else if (jX < center - deadzone) dir = -1;

  // if joystick is moved from the center, calculate how far it is from the center (effort) and map it to a delay
  if (dir != 0) {
      int effort = abs(jX - center);
      stepDelay = map(effort, deadzone, 512, 2000, 500); // slow -> fast
  }

  // if theres a direction command an enough time has passed, move stepper motor and record time accordingly
  if (dir != 0 && micros() - lastStepTime >= stepDelay) {
      if (dir == 1) stepForward();
      else stepBackward();
      lastStepTime = micros();
  }

}

void stepForward() {
  currentStep = (currentStep + 1) % 8;
  for (int i = 0; i < 4; i++) {
    digitalWrite(motorPins[i], stepSequence[currentStep][i]);
  }
}

void stepBackward() {
  currentStep = (currentStep + 7) % 8;
  for (int i = 0; i < 4; i++) {
    digitalWrite(motorPins[i], stepSequence[currentStep][i]);
  }
}
